{
  perSystem =
    {
      lib,
      config,
      pkgs,
      ...
    }:
    let
      packages = with pkgs; [
        deadnix
        nixd
        nixfmt
        python3
        statix
        (writeShellScriptBin "new-plugin" ''
          if [ $# -ne 2 ]; then
            echo "Usage: new-plugin <plugin-name> <template-type>"
            echo "Template types: custom, custom-lazy, nixvim"
            exit 1
          fi
          ${./new-plugin.py} "$1" "$2"
        '')
      ];
    in
    {
      devShells.default = pkgs.mkShell {
        name = "Khanelivim development shell";
        meta.description = "Shell environment for modifying this Nix configuration";
        inherit packages;

        shellHook = ''
          ${config.pre-commit.installationScript}

          echo "ðŸš€ Khanelivim development shell"
          echo ""
          echo "ðŸ”§ Available commands:"
          echo "  new-plugin <name> <type> - Generate new plugin template"
          echo "  deadnix -e               - Check for unused Nix code"
          echo "  statix fix .             - Fix Nix linting issues"
          echo "  nix fmt .                - Format tree"
          echo ""
          echo "ðŸ“¦ Available packages:"
          ${lib.concatMapStringsSep "\n" (
            pkg: ''echo "  - ${pkg.pname or pkg.name or "unknown"} (${pkg.version or "unknown"})"''
          ) packages}
        '';
      };
    };
}
